# NSIS installer script for for gnusim8085
# AUTHOR - Onkar Shinde <onkarshinde@gmail.com>
# LICENSE: Same as specified in COPYING file
#
# NOTE: Tested with NSIS 2.43 only. 2.43 is giving problem when trying to set
# icon for installer. You may have to disable icon if you are trying to build
# installer.

# Use modern UI
!include MUI2.nsh

# Define application name and installer file name
Name GNUSim8085
OutFile @PACKAGE@-@VERSION@-no-gtk-installer.exe
!define MUI_ICON pixmaps/gnusim8085.ico
InstallDir "C:\Program Files\GNUSim8085"
!define startmenu "$SMPROGRAMS\GNUSim8085"
!define uninstaller "uninstaller.exe"

# Show license page
!define MUI_LICENSEPAGE_CHECKBOX
!insertmacro MUI_PAGE_LICENSE COPYING

# Show installation directory selector page
!insertmacro MUI_PAGE_DIRECTORY

# Show installation progress page
!insertmacro MUI_PAGE_INSTFILES

# Show finish page with an option to launch program
!define MUI_FINISHPAGE_RUN $INSTDIR\gnusim8085.exe
!insertmacro MUI_PAGE_FINISH

# Show uninstall confirmation page
!insertmacro MUI_UNPAGE_CONFIRM

# Show uninstall progress page
!insertmacro MUI_UNPAGE_INSTFILES

# Set available UI languages for installer
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "Arabic"
!insertmacro MUI_LANGUAGE "Esperanto"
!insertmacro MUI_LANGUAGE "French"
!insertmacro MUI_LANGUAGE "German"
!insertmacro MUI_LANGUAGE "Italian"
!insertmacro MUI_LANGUAGE "Spanish"

# Show language selection dialog in installer
Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

# Install section
Section "Install"
  SetOutPath $INSTDIR
  !system 'make'
  !system 'cp /usr/i586-mingw32msvc/bin/libgtksourceview-2.0-0.dll src/'
  !system 'cp /usr/i586-mingw32msvc/bin/libxml2.dll src/'
  !system 'cp /usr/i586-mingw32msvc/share/gtksourceview-2.0/language-specs/language2.rng data/'
  !system 'cp /usr/i586-mingw32msvc/share/gtksourceview-2.0/language-specs/def.lang data/'
  !system 'cp /usr/i586-mingw32msvc/share/gtksourceview-2.0/styles/styles.rng data/'
  !system 'cp /usr/i586-mingw32msvc/share/gtksourceview-2.0/styles/classic.xml data/'
  File AUTHORS
  File ChangeLog
  File COPYING
  File NEWS
  File TODO
  File src/gnusim8085.exe
  File /r /x Makefile* data
  File /r doc/examples
  File /r /x Makefile* pixmaps
  File src/libgtksourceview-2.0-0.dll
  File src/libxml2.dll
  # Make example program files read only
  SetFileAttributes $INSTDIR\examples\addwithcarry.asm READONLY
  SetFileAttributes $INSTDIR\examples\bcd2bin.asm READONLY
  SetFileAttributes $INSTDIR\examples\debugexample.asm READONLY
  SetFileAttributes $INSTDIR\examples\nqueens.asm READONLY
  SetFileAttributes $INSTDIR\examples\swaphex.asm READONLY
  CreateDirectory ${startmenu}
  CreateShortcut "${startmenu}\GNUSim8085.lnk" "$INSTDIR\gnusim8085.exe" "" "$INSTDIR\pixmaps\gnusim8085.ico"
  WriteUninstaller $INSTDIR\${uninstaller}
  CreateShortcut "${startmenu}\Uninstall.lnk" "$INSTDIR\${uninstaller}"
  !system 'rm -f src/libgtksourceview-2.0-0.dll'
  !system 'rm -f src/libxml2.dll'
  !system 'rm -f data/language2.rng'
  !system 'rm -f data/styles.rng'
  !system 'rm -f data/classic.xml'
  !system 'rm -f data/def.lang'
SectionEnd

# Uninstall section
Section "Uninstall"
  Delete $INSTDIR\uninstaller.exe
  RMDir /r ${startmenu}
  RMDir /r $INSTDIR
SectionEnd

